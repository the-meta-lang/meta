.DATA

// This is the symbol table
// It is used to keep track of the variables and their memory locations
ST = { 65536 };

// This is the symbol table offset
STO = 12;

// This number keeps track of the number of variables in the current context frame
// e.g. the function body
VAR_IN_BODY = 0;

// This boolean checks if we are currently evaluating the second operand in a arithmetic expression.
// Without this we would have to rely on the stack instead of directly using eax and ebx in calculations.
AEXP_SEC_NUM = 0;

EAX_WAS_NUMBER = 0;
EBX_WAS_NUMBER = 0;

.SYNTAX PROGRAM

// .INCLUDE "./gyro/includes/while-statement.meta";
// .INCLUDE "./gyro/includes/let-expression.meta";
// .INCLUDE "./gyro/includes/if-statement.meta";
// .INCLUDE "./gyro/includes/arithmetic.meta";

PROGRAM = 
.LABEL("section .text")
	->("global _start")
.LABEL("_start:")
->("push ebp")
->("mov ebp, esp")
$ (LET_EXPRESSION ";" | TYPE_EXPRESSION ";" | ENUM_EXPRESSION | FN_EXPRESSION | FN_CALL ";" | IF_STATEMENT | WHILE_STATEMENT)
->("pop ebp")
->("mov eax, 4")
->("mov ebx, 0")
->("int 0x80");

FN_EXPRESSION = "fn" ->("jmp " *1) .ID .LABEL(* ":") ->("push ebp") ->("mov ebp, esp") "(" 
$(.ID [hash-set ST *last_match [set STO [+ STO 4]]] [set VAR_IN_BODY [+ VAR_IN_BODY 1]] ->("mov dword [ebp+" [STO] "], edi"))
$(("," .ID [hash-set ST *last_match [set STO [+ STO 4]]] [set VAR_IN_BODY [+ VAR_IN_BODY 1]] ->("mov dword [ebp+" [STO] "], esi"))
	$("," .ID [hash-set ST *last_match [set STO [+ STO 4]]] [set VAR_IN_BODY [+ VAR_IN_BODY 1]] ->("mov dword [ebp+" [STO] "], edx") 
		$("," .ID [hash-set ST *last_match [set STO [+ STO 4]]] [set VAR_IN_BODY [+ VAR_IN_BODY 1]] ->("mov dword [ebp+" [STO] "], ecx")
			$("," .ID [hash-set ST *last_match [set STO [+ STO 4]]] [set VAR_IN_BODY [+ VAR_IN_BODY 1]]))))
")" "{" 
	BODY 
"}" 
		[set STO [- STO [* VAR_IN_BODY 4]]]
		->("pop ebp")
		->("ret") 
		.LABEL(*1 ":");

// Function Body
BODY = $(RETURN_EXPRESSION ";" | IF_STATEMENT | WHILE_STATEMENT | LET_EXPRESSION ";" | TYPE_EXPRESSION ";" | ENUM_EXPRESSION | FN_CALL ";" | FN_EXPRESSION);

FN_CALL = .ID %> "(" FN_CALL_ARGUMENTS ")" ->("call " %);

// Function Call Arguments
// A list of arguments passed when calling a function
// @example: add(1, num) <- 1 and `num` are the arguments
FN_CALL_ARGUMENTS = 
$(.NUMBER ->("mov edi, " *))
$("," (.NUMBER ->("mov esi, " *)
	| .ID ->("mov esi, [" * "]")
	| "*" .ID ->("mov esi, " *)
	| .STRING 
		.LABEL("section .data")
			->(*3 " db " * ", 0x00")
		.LABEL("section .text")
			->("mov esi, " *3))
	$("," (.NUMBER ->("mov edx, " *)
		| .ID ->("mov edx, [" * "]")
		| "*" .ID ->("mov edx, " *)
		| .STRING 
			.LABEL("section .data")
				->(*3 " db " * ", 0x00")
			.LABEL("section .text")
				->("mov edx, " *3))
		$("," (.NUMBER ->("mov ecx, " *)
			| .ID ->("mov ecx, [" * "]")
			| "*" .ID ->("mov ecx, " *)
			| .STRING 
				.LABEL("section .data")
					->(*3 " db " * ", 0x00")
				.LABEL("section .text")
					->("mov ecx, " *3))
			$("," (.NUMBER ->("push " *)
				| .ID ->("push [" * "]")
				| "*" ->("push " *)
				| .STRING 
					.LABEL("section .data")
						->(*3 " db " * ", 0x00")
					.LABEL("section .text")
						->("push " *3))))));



// Arithmetic Expressions
// These follow the precedence set by the C language
// https://en.cppreference.com/w/c/language/operator_precedence
AEXP = EX1 [set AEXP_SEC_NUM 0] $(
	("=" EX1 
		->("mov dword [ebp+" [hash-get ST *last_match] "], eax"))
	// NOTE: Since the other operators take precedence over the arithmetic assignment operators,
	// we need to check for them first
	// This will destroy precedence though, so we need to think of a better solution...
	| ("+=" EX1 
		->("add dword [ebp+" [hash-get ST *last_match] "], eax"))
	| ("-=" EX1
		->("sub dword [ebp+" [hash-get ST *last_match] "], eax"))
	// Assignment by product, quotient, and remainder
	| ("*=" EX1
		->("mul dword [ebp+" [hash-get ST *last_match] "], eax"))
	| ("/=" EX1
		->("idiv dword [ebp+" [hash-get ST *last_match] "], eax"))
	| ("%=" EX1
		->("idiv dword [ebp+" [hash-get ST *last_match] "], eax")
		->("push edx"))
	// Assignment by bitwise left shift and right shift
	| ("<<=" EX1
		->("shl dword [ebp+" [hash-get ST *last_match] "], eax"))
	| (">>=" EX1
		->("shr dword [ebp+" [hash-get ST *last_match] "], eax"))
	// Assignment by bitwise AND, XOR, and OR
	| ("&=" EX1
		->("and dword [ebp+" [hash-get ST *last_match] "], eax"))
	| ("^=" EX1
		->("xor dword [ebp+" [hash-get ST *last_match] "], eax"))
	| ("|=" EX1
		->("or dword [ebp+" [hash-get ST *last_match] "], eax"))
);

// Ternary Conditional
EX1 = EX2 $("?" 
			->("cmp eax, 0")
			// Result is null, if so jump over the true condition
			->("je " *1)
				EX2
			// Don't invoke the false condition if the true condition is true
			->("jmp " *2) ":"
		.LABEL(*1 ":") EX2
		.LABEL(*2 ":"));

// Logical OR
EX2 = EX3 $("||" EX3 
			->("or eax, ebx"));

// Logical AND
EX3 = EX4 $("&&" EX4 
			->("and eax, ebx"));

// Bitwise OR
EX4 = EX5 $("|" EX5 
			->("or eax, ebx"));

// Bitwise XOR
EX5 = EX6 $("^" EX6 
			->("xor eax, ebx"));

// Bitwise AND
EX6 = EX7 $("&" EX7 
			->("and eax, ebx"));

// Relational == and !=
EX7 = EX8 $("==" EX9 
			->("cmp eax, ebx")
			->("je " *1)
			->("push 0")
			->("jmp " *2) 
		.LABEL(*1 ":")
			->("push 1")
		.LABEL(*2 ":")
		| "!=" EX9 
			->("cmp eax, ebx")
			->("jne " *1)
			->("push 0")
			->("jmp " *2) 
		.LABEL(*1 ":")
			->("push 1")
		.LABEL(*2 ":"));

// Comparison / Relational Operators
EX8 = EX9 $("<=" EX9 
			->("cmp eax, ebx")
			->("jle " *1)
			->("push 0")
			->("jmp " *2) 
		.LABEL(*1 ":")
			->("push 1")
		.LABEL(*2 ":")
		| ">=" EX9 
			->("cmp eax, ebx")
			->("jge " *1)
			->("push 0")
			->("jmp " *2)
		.LABEL(*1 ":")
			->("push 1")
		.LABEL(*2 ":")
		| "<" EX9 
			->("cmp eax, ebx") 
			->("jl " *1) 
			->("push 0") 
			->("jmp " *2) 
		.LABEL(*1 ":") 
			->("push 1") 
		.LABEL(*2 ":")
		| ">" EX9
			->("cmp eax, ebx")
			->("jg " *1)
			->("push 0")
			->("jmp " *2)
		.LABEL(*1 ":")
			->("push 1")
		.LABEL(*2 ":"));

// Bitwise left and right shift
EX9 = EX10 $("<<" EX10 
			->("shl eax, ebx") 
		| ">>" EX10 
			->("shr eax, ebx"));

EX10 = EX11 [set AEXP_SEC_NUM 0] $("+" [set AEXP_SEC_NUM 1] EX11   ->("add eax, ebx") 
		| "-" EX11   ->("sub eax, ebx") );

EX11 = EX12 [set AEXP_SEC_NUM 0] $("*" [set AEXP_SEC_NUM 1] EX12
			->("mul eax, ebx")
		| "/" [set AEXP_SEC_NUM 1] EX12
			->("idiv eax, ebx")
		| "%" [set AEXP_SEC_NUM 1] EX12 
			->("idiv eax, ebx")
			->("push edx"));

// TODO: Exponentiation
EX12 = EX13 [set AEXP_SEC_NUM 0] $("**" [set AEXP_SEC_NUM 1] EX12 
						->("exp"));

EX13 = "++" EX14
			->("mov eax, dword [ebp+" [hash-get ST *last_match] "]")
			->("inc eax")
			->("mov dword [ebp+" [hash-get ST *last_match] "], eax")
		| "--" EX14
			->("mov eax, dword [ebp+" [hash-get ST *last_match] "]")
			->("dec eax")
			->("mov dword [ebp+" [hash-get ST *last_match] "], eax")
		// Dereference
		| "*" .ID
			->("mov eax, dword [ebp+" [hash-get ST *last_match] "]")
			->("mov eax, dword [eax]")
		// Address of
		| "&" .ID 
			->("lea eax, [ebp+" [hash-get ST *last_match] "]")
		| "+" EX14 
		| "-" EX14 
			->("neg eax")
		| EX14;

EX14 = .ID %> (("(" $ FN_CALL_ARGUMENTS ")" ->("call " %)) 
			// Post increment operator
			| "++" 
				->("inc dword [ebp+" [hash-get ST *last_match] "]")
			// Post decrement operator
			| "--"
				->("dec dword [ebp+" [hash-get ST *last_match] "]")
			| .EMPTY ([<< AEXP_SEC_NUM] ->("mov eax, dword [ebp+" [hash-get ST *last_match] "]") | ->("mov ebx, dword [ebp+" [hash-get ST *last_match] "]") .DIRECT("call set_true")))
		| .NUMBER ([<< AEXP_SEC_NUM] ->("mov eax, " *) | ->("mov ebx, " *) .DIRECT("call set_true")) 
		| "(" AEXP ")";

// SECTION Types

BASIC_TYPE = "i8" ->("; type: i8")
	| "i16" ->("; type: i16")
	| "i32" ->("; type: i32")
	| "i64" ->("; type: i64")
	| "u8" ->("; type: u8")
	| "u16" ->("; type: u16")
	| "u32" ->("; type: u32")
	| "u64" ->("; type: u64")
	| "f32" ->("; type: f32")
	| "f64" ->("; type: f64")
	| "bool" ->("; type: bool")
	| "char" ->("; type: char");

// Union Types
// @example: i32 | f32
UNION_TYPE = (BASIC_TYPE 
	| POINTER_TYPE
	| DEREFERENCE_TYPE
	| FN_TYPE
	| ("(" COMPLEX_TYPE ")") 
	| (.ID ->("; type: " * " (alias)")) 
	| ARRAY_TYPE) 
		$("|" ->("; or") 
			(BASIC_TYPE 
			| POINTER_TYPE
			| DEREFERENCE_TYPE
			| FN_TYPE
			| ("(" COMPLEX_TYPE ")") 
			| (.ID ->("; type: " * " (alias)")) 
			| ARRAY_TYPE));

// Pointer Type
// @example: *i32
POINTER_TYPE = "*" ->("; pointer") COMPLEX_TYPE;

// Dereference Type
// @example: &i32
DEREFERENCE_TYPE = "&" ->("; dereference") COMPLEX_TYPE;

// Function Type
// @example: fn(i32, i32) -> i32

FN_TYPE = "fn" ->("; fn type") ("<" ->("; is generic") COMPLEX_TYPE ">" | .EMPTY) "(" (COMPLEX_TYPE ->("; input") $("," COMPLEX_TYPE ->("; input")) | .EMPTY) ")" "->" ->("; output") COMPLEX_TYPE;

// Array Type
// Includes both fixed length and dynamic length arrays
// @example: [i32; 4]
// @example: [i32; *]
// @example: i32[4]
// @example: i32[]
ARRAY_TYPE = ("[" (UNION_TYPE
	| POINTER_TYPE
	| DEREFERENCE_TYPE
	| BASIC_TYPE 
	| FN_TYPE) ";" (.NUMBER | "*") "]")
		| ((UNION_TYPE 
			| BASIC_TYPE 
			| FN_TYPE) "[" (.NUMBER | "*" | .EMPTY) "]");

// Complex Type
COMPLEX_TYPE = UNION_TYPE
	| POINTER_TYPE
	| DEREFERENCE_TYPE
	| BASIC_TYPE
	| FN_TYPE
	| ("(" COMPLEX_TYPE ")")
	| (.ID ->("; type: " * " (alias)"))
	| ARRAY_TYPE;

TYPE_ANNOTATION = COMPLEX_TYPE;

// SECTION Expressions

TYPE_EXPRESSION = "type" .ID ->("; define type " *) "=" COMPLEX_TYPE;

ENUM_EXPRESSION = "enum" .ID ->("; define enum " *) "{" (.ID ->("; enum value " *) | .EMPTY) $("," .ID ->("; enum value " *)) "}";

IF_STATEMENT = "if" "(" AEXP ")" 
	->("cmp eax, 0")
	->("je " *1) "{"
		BODY 
	"}"
	->("jmp " *2)
	.LABEL(*1 ":")
		$(ELIF) $(ELSE)
	.LABEL(*2 ":");

ELIF = "elif" "(" AEXP ")" 
		->("cmp eax, 0")
		->("jne " *1) "{"
			BODY 
		"}"
		.LABEL(*1 ":");

ELSE = "else" "{" 
		BODY 
	"}";

LET_EXPRESSION = "let" .ID [hash-set ST *last_match [set STO [+ STO 4]]] [set VAR_IN_BODY [+ VAR_IN_BODY 1]] (":" TYPE_ANNOTATION | .EMPTY) "=" AEXP
		->("mov dword [ebp+" [STO] "], eax");

WHILE_STATEMENT = "while" .LABEL(*2 ":") "(" AEXP ")" "{" 
		->("cmp eax, 0")
		->("je " *1)
		BODY 
	"}"
	->("jmp " *2)
	.LABEL(*1 ":");

// Return Expression
// @example: return 1;
// Set AEXP_SEC_NUM to 0 to indicate that we are evaluating the second operand in the arithmetic expression
// This will make it so that the last value will always be in eax
RETURN_EXPRESSION = "return" [set AEXP_SEC_NUM 0] AEXP;




.END
